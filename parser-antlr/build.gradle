description = 'equal5 http://antlr3.org parser'

configurations {
    antlr3
}

dependencies {
    compile project(':equal5-engine')

    compile 'org.antlr:antlr-runtime:3.5', { transitive = false }

    testCompile project(path: ':equal5-engine', configuration: 'testFixtures')

    antlr3 "org.antlr:antlr:3.5"
}

sourceSets.main.java.srcDir "$buildDir/generated-sources/antlr3"

task runAntlr {
    description = "Generates Java sources from Antlr3 grammars."

    ext.inputFile = file("src/main/antlr3/engine/expressions/parser/antlr/Equal.g")
    ext.outputDir = file("$buildDir/generated-sources/antlr3/engine/expressions/parser/antlr")

    inputs.dir inputFile
    outputs.dir outputDir
} << {
    mkdir(outputDir)

    ant.java(classname: 'org.antlr.Tool',
            classpath: "${configurations.antlr3.asPath}",
            failonerror: true) {
        arg(line: "-o ${outputDir}")
        arg(line: inputFile)
    }
}

compileJava.dependsOn runAntlr