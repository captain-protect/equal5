description = 'equal5 webstart distribution'
apply plugin: "base"

import org.apache.tools.ant.filters.ReplaceTokens

ext.dependentLib = file "$buildDir/dependent_lib";
ext.signedLib = file "$buildDir/signed_lib";
ext.standaloneDist = file "$buildDir/standaloneDist";
ext.keystoreFile = 'src/jnlp/keys/equal5.keys'
ext.password = "abc123"
ext.alias = "equal5-webstart-key"

task makeDirs << {
    file(dependentLib).mkdirs()
    file(signedLib).mkdirs()
    file(standaloneDist).mkdirs()
}

task copyDeps(type: Copy, dependsOn: makeDirs) {
    from project(":equal5-gui").configurations.runtime.allArtifacts.files
    from project(":equal5-gui").configurations.runtime
    into(dependentLib)
    include('*.jar')
}


task signAll(dependsOn: copyDeps) {
    ext.inputJars = files { file(dependentLib).listFiles() }

    inputs.file inputJars
    outputs.dir signedLib

    doLast {
        inputJars.each {
            ant.signjar(
                    destDir: signedLib,
                    alias: alias,
                    jar: it,
                    keystore: keystoreFile,
                    storepass: password,
                    preservelastmodified: 'true')
        }
    }
}

task copyToStandalone(type: Copy, dependsOn: signAll) {
    from signedLib
    into standaloneDist
}

task generateJnlp(dependsOn: copyToStandalone) {
    ext.inputFile = file("src/jnlp/equal5.jnlp");
    ext.outputFile = file("$standaloneDist/equal5.jnlp");
    ext.jarsDir = standaloneDist
    ext.jarFiles = files { file(jarsDir).listFiles() }

    inputs.file { inputFile }
    outputs.file { outputFile }

    doLast {
        def props = new Properties()
        props.put("dependencies", jarFiles.collect {
            str -> "<jar href=\"$str.name\" />"
        }.join("\n\t"))

        copy {
            from inputFile
            into outputFile
            filter(ReplaceTokens, tokens: props)
        }
    }
}

task standaloneDist(type: Zip, dependsOn: generateJnlp) {
    baseName = "equal5"
    from standaloneDist
}


artifacts {
    archives standaloneDist
}

task showVersion << {
    println
}